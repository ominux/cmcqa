
//
//  Test specifications for the r2_et_cmc model
//
//  The test parameters are selected such that 
//  - tnom zero bias resistance is 1 Ohms (conductance is 1 S)
//  - zero bias conductance variation is +/-10% at hi/lo temperatures
//  - tnom white noise is 1.6576089903e-20
//

`ifdef hspice
nTypeSelectionArguments     r2_et_cmc
pTypeSelectionArguments     r2_et_cmc
scaleParameters             scale shrink m
`endif
`ifdef spectre
nTypeSelectionArguments     r2_et_cmc
pTypeSelectionArguments     r2_et_cmc
scaleParameters             scale shrink m
`endif
`ifdef ads
nTypeSelectionArguments     r2_et_cmc
pTypeSelectionArguments     r2_et_cmc
scaleParameters             m
keyLetter                   a
`endif
`ifdef eldo
nTypeSelectionArguments     r2_et_cmc
pTypeSelectionArguments     r2_et_cmc
scaleParameters             scale shrink m
keyLetter                   y
`endif
`ifdef mica
nTypeSelectionArguments     r2_et_cmc
pTypeSelectionArguments     r2_et_cmc
scaleParameters             scale shrink m
`endif

//
//  General test-independent information
//

pins                        n1 n2
linearScale                 l w
areaScale                   as ad
temperature                 27 8.818181 49.222222
checkPolarity               yes
symmetricTerminals          n1 n2                // this triggers testing for n1 and n2 flipped
verilogAfile                ../vacode/cmcModels.va

//
//  Test specifications
//

test                        linearDcSweep_et
biases                      V(n1)=0
biasSweep                   V(n2)=0,10,0.1
outputs                     I(n2)
instanceParameters          l=1.1e-6 w=0.9e-6
modelParameters             rsh=1.0 xl=-0.1 xw=0.1 gth0=0.2 tc1=5.0e-3

test                        linearAcSweep_et
biases                      V(n1)=0
biasSweep                   V(n2)=0,10,1
outputs                     G(n2,n2)
instanceParameters          l=1.1e-6 w=0.9e-6
modelParameters             rsh=1.0 xl=-0.1 xw=0.1 gth0=0.2 tc1=5.0e-3

test                        linearNoise_et
biases                      V(n1)=0
biasSweep                   V(n2)=0,10,5
outputs                     N(n2)
frequency                   dec 10 1 1e6
instanceParameters          l=1.0e-6 w=2.0e-6
modelParameters             rsh=1.0 kfn=3.3119027626194e-19 gth0=0.2 tc1=5.0e-3 afn=1.9

test                        nonlinearDcSweep_et
biases                      V(n1)=0
biasSweep                   V(n2)=0,10,0.1
outputs                     I(n2)
instanceParameters          l=1.0e-6 w=1.0e-6
modelParameters             rsh=1.0 p3=0.121744 q3=1.63134 p2=0.0143537 q2=3.69283 gth0=0.2 tc1=5.0e-3

test                        nonlinearAcSweep_et
biases                      V(n1)=0
biasSweep                   V(n2)=0,10,1
outputs                     G(n2,n2)
instanceParameters          l=1.0e-6 w=1.0e-6
modelParameters             rsh=1.0 p3=0.121744 q3=1.63134 p2=0.0143537 q2=3.69283 gth0=0.2 tc1=5.0e-3

test                        nonlinearNoise_et
biases                      V(n1)=0
biasSweep                   V(n2)=0,10,5
outputs                     N(n2)
frequency                   dec 10 1 1e6
instanceParameters          l=1.0e-6 w=2.0e-6
modelParameters             rsh=1.0 kfn=3.3119027626194e-19 p3=0.121744 q3=1.63134 p2=0.0143537 q2=3.69283 gth0=0.2 tc1=5.0e-3 afn=1.9
